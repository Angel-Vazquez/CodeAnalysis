//%attributes = {"invisible":true}
// FormProperties_SaveToFile (formPath; formName; tableNo)
// FormProperties_SaveToFile (text; text; longint)
//
// DESCRIPTION
//   Scans the objects on the "Current" form, generates
//   a file, and saves it to disk.
//
C_LONGINT:C283($1; $vl_tableNo)  // 0 means a project form
C_TEXT:C284($2; $vt_formName)  // project/table form name
C_TEXT:C284($3; $vt_diskFilePath)  // Path to save the form properties too
// ----------------------------------------------------
// HISTORY
//   Created by: DB (10/30/2012)
//   Mod: DB (11/05/2012) - Task 2159 - Convered to JSON
//   Mod: DB (11/07/2012) - Added calls to JSON_EncodeString to handle encoding
//   Mod: DB (06/26/2013) - Use different method to fetch form properties
//   Mod: DB (09/03/2015) - Converted to C_OBJECT
// ----------------------------------------------------

If (DEV_ASSERT_PARMCOUNT(Current method name:C684; 3; Count parameters:C259))
	$vl_tableNo:=$1
	$vt_formName:=$2
	$vt_diskFilePath:=$3
	
	If ($vl_tableNo>0)
		FORM LOAD:C1103(Table:C252($vl_tableNo)->; $vt_formName; *)
	Else 
		FORM LOAD:C1103($vt_formName; *)
	End if 
	
	// Load all teh form objects from the current loaded form
	ARRAY TEXT:C222($at_objectsArray; 0)
	ARRAY POINTER:C280($ap_variablesArray; 0)
	ARRAY LONGINT:C221($vl_pagesArray; 0)
	FORM GET OBJECTS:C898($at_objectsArray; $ap_variablesArray; $vl_pagesArray)
	
	C_LONGINT:C283($vl_formWidth; $vl_formHeight; $vl_numPages)
	C_BOOLEAN:C305($vb_formFixedWidth; $vb_formFixedHeight)
	C_TEXT:C284($vt_title)
	C_BOOLEAN:C305($vb_noResult)
	Host_GetAssetInfo_GetFormProp($vl_tableNo; $vt_formName; ->$vl_formWidth; ->$vl_formHeight; ->$vl_numPages; ->$vb_formFixedWidth; ->$vb_formFixedHeight; ->$vt_title)
	
	// Define some useful vars for the json
	C_OBJECT:C1216($vo_formProperties)
	CLEAR VARIABLE:C89($vo_formProperties)
	
	// ##### FORM PROPERTIES - START
	If (True:C214)
		If ($vl_tableNo>0)
			OB SET:C1220($vo_formProperties; "FromTable"; "["+Table name:C256($vl_tableNo)+"]")
		End if 
		OB SET:C1220($vo_formProperties; "Name"; $vt_formName)
		If ($vt_title#"")
			OB SET:C1220($vo_formProperties; "Title"; $vt_title)
		End if 
		OB SET:C1220($vo_formProperties; "PageCount"; $vl_numPages)
		
		C_OBJECT:C1216($vo_tmpObj)
		CLEAR VARIABLE:C89($vo_tmpObj)
		OB SET:C1220($vo_tmpObj; "Width"; $vl_formWidth; "Height"; $vl_formHeight)
		OB SET:C1220($vo_formProperties; "Size"; $vo_tmpObj)
		
		CLEAR VARIABLE:C89($vo_tmpObj)
		OB SET:C1220($vo_tmpObj; "Width"; Choose:C955($vb_formFixedWidth; "Yes"; "No"); "Height"; Choose:C955($vb_formFixedHeight; "Yes"; "No"))
		OB SET:C1220($vo_formProperties; "FixedSize"; $vo_tmpObj)
		
		
		C_BOOLEAN:C305($vb_canResize)
		C_LONGINT:C283($vl_minSize; $vl_maxSize)
		FORM GET VERTICAL RESIZING:C1078($vb_canResize; $vl_minSize; $vl_maxSize)
		CLEAR VARIABLE:C89($vo_tmpObj)
		If ($vb_canResize)
			OB SET:C1220($vo_tmpObj; "CanResize"; $vb_canResize; "min"; $vl_minSize; "max"; $vl_maxSize)
		Else 
			OB SET:C1220($vo_tmpObj; "CanResize"; $vb_canResize)
		End if 
		OB SET:C1220($vo_formProperties; "FormVerticalResizing"; $vo_tmpObj)
		
		FORM GET HORIZONTAL RESIZING:C1077($vb_canResize; $vl_minSize; $vl_maxSize)
		If ($vb_canResize)
			OB SET:C1220($vo_tmpObj; "CanResize"; $vb_canResize; "min"; $vl_minSize; "max"; $vl_maxSize)
		Else 
			OB SET:C1220($vo_tmpObj; "CanResize"; $vb_canResize)
		End if 
		OB SET:C1220($vo_formProperties; "FormHorizontalResizing"; $vo_tmpObj)
		
	End if 
	// ##### FORM PROPERTIES - END
	
	C_OBJECT:C1216($vo_root)
	OB SET:C1220($vo_root; "FormProperties"; $vo_formProperties)
	
	
	
	C_LONGINT:C283($vlTableNum; $vlFieldNum; $vl_horzSizingOption; $vl_vertSizingOption)
	C_TEXT:C284($vt_VarName; $vt_ChoiceListName)
	C_LONGINT:C283($vl_left; $vl_top; $vl_right; $vl_bottom)
	ARRAY OBJECT:C1221($ao_pageProperties; 0)
	C_OBJECT:C1216($vo_page)
	C_LONGINT:C283($i; $j)
	For ($i; 0; $vl_numPages)
		CLEAR VARIABLE:C89($vo_page)
		OB SET:C1220($vo_page; "PageNo"; $i)
		
		
		ARRAY OBJECT:C1221($ao_ObjectProperties; 0)
		C_OBJECT:C1216($vo_Object)
		For ($j; 1; Size of array:C274($at_objectsArray))
			
			If ($vl_pagesArray{$j}=$i)
				CLEAR VARIABLE:C89($vo_Object)
				
				// Get some properties
				OBJECT GET COORDINATES:C663(*; $at_objectsArray{$j}; $vl_left; $vl_top; $vl_right; $vl_bottom)
				RESOLVE POINTER:C394($ap_variablesArray{$j}; $vt_VarName; $vlTableNum; $vlFieldNum)
				$vt_ChoiceListName:=OBJECT Get list name:C1072(*; $at_objectsArray{$j})
				OBJECT GET RESIZING OPTIONS:C1176(*; $at_objectsArray{$j}; $vl_horzSizingOption; $vl_vertSizingOption)
				
				OB SET:C1220($vo_Object; "ObjectName"; $at_objectsArray{$j})
				
				Case of 
					: ($vt_VarName="")
						// DO NOTHING
					: ($vt_VarName="$form.@")  // The "$form.@" vars seem to be dynamically generated by 4D v16.x 
						OB SET:C1220($vo_Object; "VariableName"; "")
					: ($vt_VarName="$form_@")  // The "$form.@" vars seem to be dynamically generated by 4D v17.x
						OB SET:C1220($vo_Object; "VariableName"; "")
					Else 
						OB SET:C1220($vo_Object; "VariableName"; $vt_VarName)
				End case 
				
				Case of 
					: ($vlTableNum>0) & ($vlFieldNum>0)
						OB SET:C1220($vo_Object; "FieldName"; "["+Table name:C256($vlTableNum)+"]"+Field name:C257($vlTableNum; $vlFieldNum))
						
					: ($vlTableNum>0)
						OB SET:C1220($vo_Object; "TableName"; "["+Table name:C256($vlTableNum)+"]")
				End case 
				
				C_TEXT:C284($vt_objText; $vl_indent5; $vl_indent4)
				$vt_objText:=""
				
				C_OBJECT:C1216($vo_tmpObj)
				CLEAR VARIABLE:C89($vo_tmpObj)
				OB SET:C1220($vo_tmpObj; "Top"; $vl_top; "Left"; $vl_left; "Bottom"; $vl_bottom; "Right"; $vl_right)
				OB SET:C1220($vo_Object; "BoundingRect"; $vo_tmpObj)
				
				OB SET:C1220($vo_Object; "Visible"; OBJECT Get visible:C1075(*; $at_objectsArray{$j}))
				//OB SET($vo_Object;"Enabled";OBJECT Get enabled(*;$at_objectsArray{$j})) 
				OB SET:C1220($vo_Object; "Enterable"; OBJECT Get enterable:C1067(*; $at_objectsArray{$j}))
				
				If ($vt_ChoiceListName#"")
					OB SET:C1220($vo_Object; "ChoiceList"; $vt_ChoiceListName)
				End if 
				
				C_OBJECT:C1216($vo_tmpObj)
				CLEAR VARIABLE:C89($vo_tmpObj)
				OB SET:C1220($vo_tmpObj; "Horizontal"; $vl_horzSizingOption; "Vertical"; $vl_vertSizingOption)
				OB SET:C1220($vo_Object; "SizingOptions"; $vo_tmpObj)
				
				// Title
				C_TEXT:C284($vt_objectTitle)
				$vt_objectTitle:=OBJECT Get title:C1068(*; $at_objectsArray{$j})
				If ($vt_objectTitle#"")
					OB SET:C1220($vo_Object; "Title"; $vt_objectTitle)
				End if 
				
				// Format
				C_TEXT:C284($vt_property; $vt_property2)
				$vt_property:=OBJECT Get format:C894(*; $at_objectsArray{$j})
				If ($vt_property#"") & ($vt_property#$vt_objectTitle)
					OB SET:C1220($vo_Object; "Format"; $vt_property)
				End if 
				
				// Filter
				$vt_property:=OBJECT Get filter:C1073(*; $at_objectsArray{$j})
				If ($vt_property#"")
					OB SET:C1220($vo_Object; "Filter"; $vt_property)
				End if 
				
				// Alignment
				$vt_property:=String:C10(OBJECT Get horizontal alignment:C707(*; $at_objectsArray{$j}))
				$vt_property2:=String:C10(OBJECT Get vertical alignment:C1188(*; $at_objectsArray{$j}))
				If ($vt_property#"0") | ($vt_property2#"0")
					$vt_property:=Choose:C955(Num:C11($vt_property); ""; "Align default"; "Align left"; "Align center"; "Align right")
					$vt_property2:=Choose:C955(Num:C11($vt_property2); ""; "Align default"; "Align top"; "Align center"; "Align bottom")
					
					C_OBJECT:C1216($vo_tmpObj)
					CLEAR VARIABLE:C89($vo_tmpObj)
					If ($vt_property#"")
						OB SET:C1220($vo_tmpObj; "Horizontal"; $vt_property)
					End if 
					If ($vt_property2#"")
						OB SET:C1220($vo_tmpObj; "Vertical"; $vt_property2)
					End if 
					OB SET:C1220($vo_Object; "Alignment"; $vo_tmpObj)
				End if 
				
				// Colour
				C_LONGINT:C283($vl_foregroundColor; $vl_backgroundColor; $vl_altBackgrndColor)
				OBJECT GET RGB COLORS:C1074(*; $at_objectsArray{$j}; $vl_foregroundColor; $vl_backgroundColor; $vl_altBackgrndColor)
				C_OBJECT:C1216($vo_tmpObj)
				CLEAR VARIABLE:C89($vo_tmpObj)
				OB SET:C1220($vo_tmpObj; "Foreground"; STR_LongintToHexString($vl_foregroundColor; 8); "Background"; STR_LongintToHexString($vl_backgroundColor; 8); "Alt Background"; STR_LongintToHexString($vl_altBackgrndColor; 8))
				OB SET:C1220($vo_Object; "ColorRGB"; $vo_tmpObj)
				
				// Scrollbar
				C_BOOLEAN:C305($vb_horizontal; $vb_vertical)
				OBJECT GET SCROLLBAR:C1076(*; $at_objectsArray{$j}; $vb_horizontal; $vb_vertical)
				If ($vb_horizontal | $vb_vertical)
					C_OBJECT:C1216($vo_tmpObj)
					CLEAR VARIABLE:C89($vo_tmpObj)
					OB SET:C1220($vo_tmpObj; "Horizontal"; Choose:C955($vb_horizontal; "Displayed"; "Hidden"); "Vertical"; Choose:C955($vb_vertical; "Displayed"; "Hidden"))
					OB SET:C1220($vo_Object; "Scrollbar"; $vo_tmpObj)
				End if 
				
				// Help Tip
				$vt_property:=OBJECT Get help tip:C1182(*; $at_objectsArray{$j})
				If ($vt_property#"")
					OB SET:C1220($vo_Object; "HelpTip"; $vt_property)
				End if 
				
				// Font Properties
				C_OBJECT:C1216($vo_tmpObj)
				CLEAR VARIABLE:C89($vo_tmpObj)
				OB SET:C1220($vo_tmpObj; "Name"; OBJECT Get font:C1069(*; $at_objectsArray{$j}); "Size"; OBJECT Get font size:C1070(*; $at_objectsArray{$j}); "Style"; OBJECT Get font style:C1071(*; $at_objectsArray{$j}))
				OB SET:C1220($vo_Object; "Font"; $vo_tmpObj)
				
				$vt_objText:=$vt_objText+$vl_indent4+"}"
				
				APPEND TO ARRAY:C911($ao_ObjectProperties; $vo_Object)
			End if 
			
		End for 
		OB SET ARRAY:C1227($vo_page; "Objects"; $ao_ObjectProperties)
		
		
		APPEND TO ARRAY:C911($ao_pageProperties; $vo_page)
	End for 
	OB SET ARRAY:C1227($vo_root; "PageProperties"; $ao_pageProperties)
	
	
	FORM UNLOAD:C1299
	
	C_TEXT:C284($vt_msg)
	$vt_msg:=JSON Stringify:C1217($vo_root; *)
	
	File_Delete($vt_diskFilePath)
	
	C_TIME:C306($fileRef)
	$fileRef:=Create document:C266($vt_diskFilePath)
	If (OK=1)
		C_BLOB:C604($vx_theCode)
		CONVERT FROM TEXT:C1011($vt_msg; "UTF-8"; $vx_theCode)  // Methods will be saved as UTF-8 so I need to convert the 4D Text (UTF-16) to UTF-8...
		SEND PACKET:C103($fileRef; UTF8_BOMString)  // Insert the BOM...
		SEND PACKET:C103($fileRef; $vx_theCode)
		CLOSE DOCUMENT:C267($fileRef)
	End if 
	
End if   // ASSERT